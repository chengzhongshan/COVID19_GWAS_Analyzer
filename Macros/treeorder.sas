%macro treeorder(
data=, 	/*A typical output generated by proc cluster*/
child=child,  /*Equivalent to y_name_ or x_name_ from proc cluster; ensure there is not spaces
included in the elements of this variable!*/
parent=parent, /*Equivalent to y_parent_ or x_parent_ from proc cluster*/ 
hh=hh, /*Equivalent to y_height_ or x_height_ from proc cluster*/
root=CL1, /*the toppest root cluster name, and all braches under it will be searched for leaves*/
order=ASC, /*Any non-string would be OK, as if provided with the string NAME, the input data
will be sorted by parent and child names, however, SAS generted tree data set is used by 
proc tree or the statement of dendrogram in proc template to draw tree by sorting 
with the modified parent by using numers to represnet them, 
child and height, so please just use default string ASC for SAS generated input data*/ 
outds=leaf_order, /*An output data set containing ordered leaf names from left to right in the dendrogram
if running with SAS to generate tree using the input data*/
parent_ds=parent_list /*An output data set contain all parents and its childrens*/
);
  %local tmp parents rc;
  data _tree_src;
  set &data;
  *Use numeric parent to order it;
  _parent_=prxchange("s/^CL//",-1,&parent)+0;
  run;
  /* 1) ensure sorted children by chosen order (HH or NAME) */
  %if %upcase(&order)=NAME %then %do;
     proc sort data=_tree_src ; by _parent_ &child; run;
  %end;
  %else %do;
     /* order by height (hh) then child name */
     proc sort data=_tree_src; by _parent_ &child &hh; run;
  %end;

  /* 2) create a parent -> space-separated children list */
  data _parent_list(keep=&parent children _parent_);
     length children $ 32767;
     retain children;
     set _tree_src;
     by _parent_;
     if first._parent_ then children = strip(&child);
     else children = catx(' ', children, strip(&child));
     if last._parent_ then output;
  run;

  /* 3) recursive traversal macro that builds a global macro variable &LEAFSEQ */
  %global LEAFSEQ;
  %let LEAFSEQ=;
   %global PARENTSEQ;
  %let PARENTSEQ=;
  %macro _traverse(node);
     %local children nxt i child sqlobs;
     proc sql noprint;
        select children into :children trimmed
        from _parent_list
        where strip(&parent) = "%bquote(&node)"
        order by children;
     quit;

     %if %eval(&sqlobs = 0) or %eval(%length(&children) = 0) %then %do;
        /* node is a leaf (no children entry) */
        %let LEAFSEQ=&LEAFSEQ %bquote(&node);
     %end;
     %else %do;
	    %let PARENTSEQ=&PARENTSEQ %bquote(&node);
        %let i=1;
        %do %while(%scan(&children,&i,%str( )) ne );
          %let child=%scan(&children,&i,%str( ));
          %_traverse(&child);
          %let i=%eval(&i+1);
        %end;
     %end;
  %mend _traverse;

  /* 4) invoke traversal starting at the root */
  %_traverse(&root);

  /* 5) create output dataset with order index and name */
  data &outds;
     length name $200;
     i=1;
     do while(scan("&LEAFSEQ",i,' ') ne '');
        name = scan("&LEAFSEQ",i,' ');
        order = i;
        output;
        i+1;
     end;
     drop i;
  run;
  proc sort data=&outds;by order name;

  /*
  data &parent_ds;
  set _parent_list;
  run;
  */

   data &outds.4parents;
     length name $200;
     i=1;
     do while(scan("&PARENTSEQ",i,' ') ne '');
        name = scan("&PARENTSEQ",i,' ');
        order = i;
        output;
        i+1;
     end;
     drop i;
  run;
  proc sort data=&outds.4parents nodupkeys;by order name;
  data &parent_ds;
  set &outds.4parents;
  run;

  /*
  proc print data=&outds noobs; title "Leaf order from root=&root"; run;
  */

  /* cleanup */
/*  proc datasets library=work nolist;*/
/*     delete _tree_src _parent_list;*/
/*  quit;*/
%mend treeorder;

/*Demo codes:;
data a;
input child :$200. parent :$200. hh;
cards;
NG_MYH11__CBFB	CL7	0
NTU_MYH11__CBFB	CL7	0
NG_KMT2Ar	CL6	0
NTU_KMT2Ar	CL6	0
NG_APL	CL5	0
NTU_APL	CL5	0
NG_RUNX1__RUNX1T1	CL4	0
NTU_RUNX1__RUNX1T1	CL4	0
CL5	CL3	0.48486
CL6	CL3	0.36378
CL3	CL2	0.90933
CL4	CL2	0.50744
CL2	CL1	1.12322
CL7	CL1	0.25102
CL1	 .	1.60468
;

 %treeorder(data=a, child=child, parent=parent, hh=hh, root=CL2, order=ASC); 

*/
