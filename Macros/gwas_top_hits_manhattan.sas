%macro gwas_top_hits_manhattan(
GWAS_File=,
Marker_Col_Name=,
Marker_Pos_Col_Name=,
Xaxis_Col_Name=,
Yaxis_Col_Name=,
Marker_X_Xpos_Y_ColNums=,
GWAS_dsdout=,
gwas_thrsd=,
Mb_SNPs_Nearby=,/*this Mb_factor will multiple 1,000,000 when running in the program*/
OutDir=
);
   %let Marker_Col_Name=%assign_str4missing(Inval=&Marker_Col_Name,NewVal=SNP);
   %let Xaxis_Col_Name=%assign_str4missing(Inval=&Xaxis_Col_Name,NewVal=Chr);
   %let Yaxis_Col_Name=%assign_str4missing(Inval=&Yaxis_Col_Name,NewVal=P);
   %let Marker_Pos_Col_Name=%assign_str4missing(Inval=&Marker_Pos_Col_Name,NewVal=BP);
   %let GWAS=&GWAS_File;
   
   %let OutName=%scan(&GWAS,-1,'\/');
   %let OutName=%sysfunc(prxchange(s/\W+/_/,-1,&OutName));
   %put Your output file prefix is &OutName;
   %let curdir=%curdir;
   
   %if %FileOrDirExist(&curdir/&OutDir/&OutName..html) %then %do;
    %put There are previous result for your manhattan pipeline, the fullpath of which is &OutName..html;
	  %put SAS will not run the analysis again only if you delete the above html file;
    %return;
   %end;
   %else %do;      
   
   /*Import GWAS data*/
   *options mlogic mprint symbolgen;
   %if %eval("&Marker_X_Xpos_Y_ColNums"="") %then %do;
   
   %ImportFilebyScan(file=&GWAS
                    ,dsdout=&GWAS_dsdout
                    ,firstobs=1
                    ,dlm='09'x
                    ,ImportAllinChar=1
                    ,MissingSymb=NaN
   );
   %end;
   %else %do;
   %ImportFilebyScanAtSpecCols(file=&GWAS
                 ,dsdout=&GWAS_dsdout
                 ,firstobs=1
                 ,dlm='09'x
                 ,ImportAllinChar=1
                 ,MissingSymb=NaN
				         ,SpeColNums=&Marker_X_Xpos_Y_ColNums
   );
   %end;
   
   %char2num_dsd(dsdin=&GWAS_dsdout,
                 vars=&Xaxis_Col_Name &Yaxis_Col_Name &Marker_Pos_Col_Name,
                 dsdout=&GWAS_dsdout);
   
   *Add 'Top' to macro var GWAS_File;
   %let GWAS=Top_&GWAS_File;
   %put Now we are going to keep SNPs &Mb_SNPs_Nearby Mb up/downstream of GWS hits;
   data tops;
   set &GWAS_dsdout;
   where -log10(&Yaxis_Col_Name)>=&gwas_thrsd;
   run;

   *Merge these top signals and only keep the most significant signals around specific dist;
   %get_top_signal_within_dist(dsdin=tops
                           ,grp_var=chr
                           ,signal_var=P
                           ,pos_var=BP
                           ,pos_dist_thrshd=&Mb_SNPs_Nearby*1000000
                           ,dsdout=tops);

   proc sql;
   create table &GWAS_dsdout(where=(tmp^="")) as
   select a.*,b.&Marker_Col_Name as tmp
   from &GWAS_dsdout as a
   right join
   tops as b
   on    a.&Xaxis_Col_Name=b.&Xaxis_Col_Name and 
         a.&Marker_Pos_Col_Name between 
         (b.&Marker_Pos_Col_Name-&Mb_SNPs_Nearby*1000000*0.5) and (b.&Marker_Pos_Col_Name+&Mb_SNPs_Nearby*1000000*0.5)
   order by a.&Xaxis_Col_Name,
         a.&Marker_Pos_Col_Name;
   data &GWAS_dsdout;
   set &GWAS_dsdout;
   drop tmp;
   run;         
        
   *Remove duplicates generated by the above codes;
   proc sort nodupkeys;by _all_;run;
   
   %mkdir(dir=&OutDir);
   %put Will go into the dir: &OutDir;     
   %if (&sysscp=WIN) %then %do;
   x cd "&OutDir";
   %end;
   %else %do;
   x cd &OutDir; 
   %end;
   /*%abort 255;*/
   %let curdir1=%curdir;
   %put Now current working dir is: &curdir1;
   

   ods html style=listing
            path="&curdir1" (url=none)
   		      body="&OutName..html";

   proc sort data=&GWAS_dsdout;by &Xaxis_Col_Name &Marker_Pos_Col_Name;run;

   %manhattan(dsdin=&GWAS_dsdout,pos_var=&Marker_Pos_Col_Name,chr_var=&Xaxis_Col_Name,P_var=&Yaxis_Col_Name,logP=1,gwas_thrsd=&gwas_thrsd);
   
   %QQplot(dsdin=&GWAS_dsdout,P_var=&Yaxis_Col_Name);  
   *%Lambda_From_P(P_dsd=&GWAS_dsdout,P_var=&Yaxis_Col_Name,case_n=,control_n=,dsdout=&GWAS_dsdout._lambda);
   %ds2csv(data=&GWAS_dsdout,runmode=b,csvfile="&OutName..csv");
   x "cd &curdir";
   
%end;

%mend;

/*
options mautolocdisplay sasautos=('/gpfs/loomis/project/fas/gelernter/zc254/SAS/SAS-Useful-Codes/Macros'
sasautos);

proc import datafile="female.FI13_subset_inclusion_logic.txt" dbms=tab out=Assoc replace;
getnames=yes;guessingrows=1000000;
run;
*/

***************Demo 1:;
/*

%let MacroDir=/project/fas/gelernter/zc254/SAS/SAS-Useful-Codes/Macros;
%include "&MacroDir/ImportAllMacros.sas";
%ImportAllMacros(MacroDir=&MacroDir,filergx=.*.sas);

%list_files4dsd(
dir=/gpfs/loomis/project/fas/gelernter/zc254/Yale_GWAS/OP_GWAS/UKB_Opioid/UKB_Word_GWAS,
file_rgx=txt,
dsdout=x
);

data x;
set x;
outname=scan(fullpath,-1,'\/');outname=prxchange('s/\.txt//',-1,outname);
outname=prxchange('s/(\W|_)//',-1,outname);
run;

*options mprint mlogic symbolgen;
*Do not provide column number to keep allele info for annotating with ANOVAR;
*Marker_X_Xpos_Y_ColNums=1 7 8 6;

data _null_;
set x(obs=1);
rc=dosubl(
'%gwas_top_hits_manhattan('||
'GWAS_File='||fullpath||
',Marker_Col_Name=SNP'||
',Marker_Pos_Col_Name=BP'||
',Xaxis_Col_Name=Chr'||
',Yaxis_Col_Name=P'||
',Marker_X_Xpos_Y_ColNums='||
',GWAS_dsdout=Assoc'||
',gwas_thrsd=7.3'||
',Mb_SNPs_Nearby=1'||
',OutDir='||outname||
');'
);
run;

*/

***************Demo 2:;

/*For EWAS:
%let MacroDir=/project/fas/gelernter/zc254/SAS/SAS-Useful-Codes/Macros;
%include "&MacroDir/ImportAllMacros.sas";
%ImportAllMacros(MacroDir=&MacroDir,filergx=.*.sas);

*x cd "E:\Coorperator_projects\EssentialGenes";
x cd /gpfs/loomis/pi/gelernter/zc254/Yale_GWAS/MetaXcan/MetaXcan/software;
proc import datafile="both_sexes.Number_of_self_reported_cancers.smultixcan.txt" dbms=tab out=Assoc replace;
getnames=yes;guessingrows=100000;
run;
proc import datafile="Ensembl_genes_Pos.txt" dbms=tab out=Ano replace;
getnames=No;guessingrows=100000;
run;
proc sql;
create table EWAS as
select a.gene_name,a.pvalue as P,
       b.var2 as chr,b.var3 as BP
from Assoc as a,
     Ano as b
where upper(scan(a.gene,1,'.'))=upper(b.var1);
proc export data=EWAS outfile="EWAS.txt" dbms=tab replace;
run;

*options mlogic symbolgen mprint;
%gwas_top_hits_manhattan(
GWAS_File=EWAS.txt,
Marker_Col_Name=gene_name,
Marker_Pos_Col_Name=BP,
Xaxis_Col_Name=Chr,
Yaxis_Col_Name=P,
Marker_X_Xpos_Y_ColNums=,
gwas_thrsd=5,
Mb_SNPs_Nearby=1,
GWAS_dsdout=EWAS_tmp,
OutDir=.
);



**************Demo 3:;
x cd "Z:\ResearchHome\ClusterHome\zcheng\St_Jude_Projects\Influenza_GWAS_UK";

%File_Head(filename="both_sexes.INFLUENZA_All_influenza_not_pneumonia_.txt",n=10);

%gwas_top_hits_manhattan(
GWAS_File=both_sexes.INFLUENZA_All_influenza_not_pneumonia_.txt,
Marker_Col_Name=SNP,
Marker_Pos_Col_Name=BP,
Xaxis_Col_Name=CHR,
Yaxis_Col_Name=P,
Marker_X_Xpos_Y_ColNums=1 7 8 6,
GWAS_dsdout=x,
gwas_thrsd=6,
Mb_SNPs_Nearby=1,
OutDir=.
);

proc sort data=x;
by chr bp;
run;
data st_end;
set x;
if first.chr then output;
else if last.chr then output;
by chr;
run;
proc freq data=x;
table chr;
run;


*/
